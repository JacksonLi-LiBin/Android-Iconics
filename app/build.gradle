apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
/*
 * Copyright (c) 2019 Mike Penz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ignored) {
}

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        multiDexEnabled true

        setProperty("archivesBaseName", "Android Iconics-v$versionName-c$versionCode")
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ignored) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ignored) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
        exclude 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.cardview:cardview:${versions.cardview}"

    // used to base on some backwards compatible themes
    // contains util classes to support various android versions, and clean up code
    // comes with the awesome "Holder"-Pattern
    // https://github.com/mikepenz/Materialize
    implementation 'com.mikepenz:materialize:1.2.1'

    // used to fill the RecyclerView with the DrawerItems
    // and provides single and multi selection, expandable items
    // https://github.com/mikepenz/FastAdapter
    implementation 'com.mikepenz:fastadapter:4.0.0'
    implementation 'com.mikepenz:fastadapter-extensions-utils:4.0.0'
    implementation 'com.mikepenz:fastadapter-extensions-ui:4.0.0'
    implementation 'com.mikepenz:fastadapter-extensions-expandable:4.0.0'

    // used to generating string fields for icons (sample - test/java/StringFieldGenerator.java)
    // https://github.com/zTrap/Android-Iconics-String-Generator
    testImplementation 'ru.ztrap.iconics:string-generator:1.0.4'

    // used to generate the drawer on the left
    // https://github.com/mikepenz/MaterialDrawer
    implementation('com.mikepenz:materialdrawer:7.0.0-rc01') {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "fastadapter-extensions-expandable"
        exclude module: "iconics-core"
    }

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    implementation('com.mikepenz:aboutlibraries:7.0.0') {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "iconics-core"
    }

    implementation project(':library-core')
    implementation project(':iconics-view-library')
    implementation project(':fontawesome-typeface-library')
    implementation project(':material-design-iconic-typeface-library')
    implementation project(':google-material-typeface-library')
    implementation project(':meteocons-typeface-library')
    implementation project(':weather-icons-typeface-library')
    implementation project(':octicons-typeface-library')
    implementation project(':community-material-typeface-library')
    implementation project(':entypo-typeface-library')
    implementation project(':typeicons-typeface-library')
    implementation project(':devicon-typeface-library')
    implementation project(':foundation-icons-typeface-library')
    implementation project(':ionicons-typeface-library')
    implementation project(':pixeden-7-stroke-typeface-library')

    testImplementation 'junit:junit:4.12'
    implementation "androidx.core:core-ktx:$versions.ktx.core"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
}
